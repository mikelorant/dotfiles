# -*- mode: shell-script -*-

## instant prompt
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

autoload -Uz compinit bracketed-paste-magic
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then compinit; else compinit -C; fi;

# plugin loader
[[ -e ~/.antidote ]] || git clone https://github.com/mattmc3/antidote.git ~/.antidote
. ~/.antidote/antidote.zsh
zstyle ':antidote:bundle' use-friendly-names 'yes'

antidote load

[ -f ~/.p10k.zsh ] && source ~/.p10k.zsh                                         # p10k configuration
[ -f ~/.fresh/build/shell.sh ] && source ~/.fresh/build/shell.sh                 # fresh
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh                                           # fzf
[ -f /Applications/iTerm.app/Contents/Resources/iterm2_shell_integration.zsh ] && source /Applications/iTerm.app/Contents/Resources/iterm2_shell_integration.zsh
[ -f /usr/local/opt/switch/switch.sh ] && source /usr/local/opt/switch/switch.sh # switch

. /usr/local/opt/asdf/libexec/asdf.sh

eval "$(direnv hook zsh)"
eval "$(zoxide init zsh --cmd cdd)"

## extra completions
[ ! -f ${fpath[1]}/_kubectl ] && kubectl completion zsh > ${fpath[1]}/_kubectl

## history file
HISTFILE=~/.zhistory
HISTSIZE=100000000
SAVEHIST=$HISTSIZE

## history command
setopt HIST_IGNORE_ALL_DUPS # history ignore duplicates
setopt HIST_IGNORE_SPACE    # history ignore commands with leading space
setopt HIST_REDUCE_BLANKS   # remove superfluous blanks from each command line being added to the history list
setopt SHARE_HISTORY        # shared history between sessions

setopt INTERACTIVECOMMENTS  # allow interactive comments
setopt TRANSIENT_RPROMPT    # remove any right prompt from display when accepting a command line
setopt COMPLETE_IN_WORD     # completion is done from both ends
setopt ALWAYS_TO_END        # the cursor is moved to the end of the word if either a single match is inserted or menu completion is performed
setopt LIST_PACKED          # try to make the completion list smaller by printing the matches in columns with different widths

## keyboard bindings
bindkey "^[[3~"   delete-char
bindkey "^[3;5~"  delete-char
bindkey "^I"      fzf-tab-partial
bindkey "^U"      backward-kill-line # https://github.com/gableroux/iterm2-macos-dynamic-profile
bindkey "^X\\x7f" backward-kill-line
bindkey "^X^_"    redo

## environmental variables
export PATH="$PATH:/usr/local/sbin"                         # add path for homebrew
export PATH="$PATH:$HOME/go/bin"                            # add path for go
export PATH="$PATH:/usr/local/opt/mysql-client/bin"         # add path for mysql-client

export ABBR_AUTOLOAD=0                          # disable updates to the user abbrevations file
export ABBR_QUIET=1                             # enable abbr quiet mode
export HOMEBREW_NO_AUTO_UPDATE=1                # do not automatically update before running
export CLICOLOR=1                               # enable colours in terminal
export DIRENV_LOG_FORMAT=                       # disable direnv output
export EDITOR=nano                              # default editor nano
export FZF_DEFAULT_OPTS="--ansi --height=33% --bind 'backward-eof:abort' --color=dark,fg:-1,bg:-1,hl:#5fff87,fg+:-1,bg+:-1,hl+:#c930c7,info:#af87ff,prompt:#5fff87,pointer:#ff87d7,marker:#ff87d7,spinner:#ff87d7"
export FZF_TMUX_HEIGHT="33%"
export GIT_COMPLETION_CHECKOUT_NO_GUESS=1       # Do not include "DWIM" suggestions in git-checkout and git-switch completion
export KUBECTL_EXTERNAL_DIFF="$HOME/.kube/kubectl_external_diff.sh"
export LESS="--quit-if-one-screen"              # automatically exit if the entire file can be displayed on the first screen
export LS_COLORS="$(vivid generate jellybeans)" # generate a LS_COLORS expression
# export LS_COLORS="di=36:ln=35:so=31;1;44:pi=30;1;44:ex=1;31:bd=0;1;44:cd=37;1;44:su=37;1;41:sg=30;1;43:tw=30;1;42:ow=30;1;43"
export LSCOLORS=gxfxbEaEBxxEhEhBaDaCaD          # colour settings for ls
export ZSH_AUTOSUGGEST_USE_ASYNC=               # disable autosuggestion async
export ZSH_AUTOSUGGEST_CLEAR_WIDGETS=($ZSH_AUTOSUGGEST_CLEAR_WIDGETS fzf-tab-partial)
export LISTMAX=0                                # number of matches to list without asking first
export _ZO_EXCLUDE_DIRS=$HOME
export _ZO_FZF_OPTS="$FZF_DEFAULT_OPTS --layout=reverse"

## aliases
alias as="asdf shell"
alias cat="bat"
alias df="df -blh"
alias grep="grep --color=auto"
alias less="bat --paging=auto"
alias ls="lsd-symbolic-link-dereference --group-dirs first --date relative"

## functions
function fzf-tab-partial() { CURRENTWORD="${LBUFFER/* /}${RBUFFER/ */}"; if [[ ($LASTWIDGET = 'fzf-tab-partial' && $_lastcomp[nmatches] -ge 5) || -z $CURRENTWORD ]]; then fzf-tab-complete; else zle complete-word; fi }
function lsd-symbolic-link-dereference() { if [[ -h ${@: -1} && -d $( greadlink -f ${@: -1} ) ]]; then lsd ${@:1:${#}-1} $( greadlink -f ${@: -1} ); else lsd ${@}; fi }
function up-line-or-history() { zle set-local-history 1; zle .up-line-or-history; zle set-local-history 0; }
function down-line-or-history() { zle set-local-history 1; zle .down-line-or-history; zle set-local-history 0; }

## zstyle
zstyle ':completion:*'          list-colors ${(s.:.)LS_COLORS}        # use ls colors
zstyle ':completion:*'          matcher-list '' 'm:{a-zA-Z}={A-Za-z}' # case insensitive
zstyle ':completion:*'          menu select                           # menu select
zstyle ':bracketed-paste-magic' active-widgets '.self-*'              # fixes slow paste

## zle
zle -N fzf-tab-partial
zle -N up-line-or-history
zle -N down-line-or-history
zle -N bracketed-paste bracketed-paste-magic

zle_highlight=(region:bold special:bold suffix:bold isearch:underline paste:bold)

[[ ${ITERM_SESSION_ID:3:1} -eq 0 && -x $(command -v neofetch) ]] && neofetch || true
