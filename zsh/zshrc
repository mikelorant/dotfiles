# -*- mode: shell-script -*-

## profiling
zmodload zsh/zprof

## instant prompt
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

autoload -Uz compinit
if [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ~/.zcompdump) ]; then compinit; else compinit -C; fi

## plugins
source <(antibody init)

antibody bundle < ~/.zsh_plugins.txt

[ -f ~/.p10k.zsh ] && source ~/.p10k.zsh                                         # p10k configuration
[ -f ~/.fresh/build/shell.sh ] && source ~/.fresh/build/shell.sh                 # fresh
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh                                           # fzf
[ -f ~/.iterm2_shell_integration.zsh ] && source ~/.iterm2_shell_integration.zsh # iterm2
# [ -f /Applications/iTerm.app/Contents/Resources/iterm2_shell_integration.zsh ] && source /Applications/iTerm.app/Contents/Resources/iterm2_shell_integration.zsh

eval "$(direnv hook zsh)"
eval "$(zoxide init zsh --cmd cdd)"

## completions
source <(kubectl completion zsh)
source <(helm completion zsh)

## history file
HISTFILE=~/.zhistory
HISTSIZE=100000000
SAVEHIST=$HISTSIZE

## history command
setopt HIST_IGNORE_ALL_DUPS # history ignore duplicates
setopt HIST_IGNORE_SPACE    # history ignore commands with leading space
setopt HIST_REDUCE_BLANKS   # remove superfluous blanks from each command line being added to the history list
setopt SHARE_HISTORY        # shared history between sessions

setopt INTERACTIVECOMMENTS  # allow interactive comments
setopt TRANSIENT_RPROMPT    # remove any right prompt from display when accepting a command line
setopt COMPLETE_IN_WORD     # completion is done from both ends
setopt ALWAYS_TO_END        # the cursor is moved to the end of the word if either a single match is inserted or menu completion is performed

## keyboard bindings
bindkey "^[[3~"   delete-char
bindkey "^[3;5~"  delete-char
bindkey "^I"      fzf-tab-partial-and-complete
bindkey "^U"      backward-kill-line # https://github.com/gableroux/iterm2-macos-dynamic-profile
bindkey "^X\\x7f" backward-kill-line
bindkey "^X^_"    redo

## environmental variables
export PATH="/usr/local/sbin:$PATH"                         # add path for homebrew
# export PATH="/usr/local/opt/findutils/libexec/gnubin:$PATH" # add path for findutils
# export PATH="/usr/local/opt/coreutils/libexec/gnubin:$PATH" # add path for coreutils
export PATH="/usr/local/opt/mysql-client/bin:$PATH"         # add path for mysql-client

export _ZO_EXCLUDE_DIRS=$HOME
export ABBR_AUTOLOAD=0                          # disable updates to the user abbrevations file
export ABBR_QUIET=1                             # enable abbr quiet mode
export HOMEBREW_NO_AUTO_UPDATE=1                # do not automatically update before running
export CLICOLOR=1                               # enable colours in terminal
export DIRENV_LOG_FORMAT=                       # disable direnv output
export EDITOR=nano                              # default editor nano
export FZF_DEFAULT_OPTS="--ansi --bind 'backward-eof:abort' --color=dark,fg:-1,bg:-1,hl:#5fff87,fg+:-1,bg+:-1,hl+:#c930c7,info:#af87ff,prompt:#5fff87,pointer:#ff87d7,marker:#ff87d7,spinner:#ff87d7"
export GIT_COMPLETION_CHECKOUT_NO_GUESS=1       # Do not include "DWIM" suggestions in git-checkout and git-switch completion
export KUBECTL_EXTERNAL_DIFF="$HOME/.kube/kubectl_external_diff.sh"
export LESS="--quit-if-one-screen"              # automatically exit if the entire file can be displayed on the first screen
export LS_COLORS="$(vivid generate jellybeans)" # generate a LS_COLORS expression
# export LS_COLORS="di=36:ln=35:so=31;1;44:pi=30;1;44:ex=1;31:bd=0;1;44:cd=37;1;44:su=37;1;41:sg=30;1;43:tw=30;1;42:ow=30;1;43"
export LSCOLORS=gxfxbEaEBxxEhEhBaDaCaD          # colour settings for ls

## aliases
alias cat="bat"
alias df="df -blh"
alias grep="grep --color=auto"
alias less="bat --paging=auto"
alias ls="lsd-symbolic-link-dereference --group-dirs first --date relative"

## functions
function iterm2-kubernetes-context() { it2setkeylabel set status "Context: $( awk '$1 == "current-context:" {print $2}' ~/.kube/config )"; }
function fzf-tab-expand-or-menu() { if [[ $_lastcomp[nmatches] -ge 5 || -z $CURRENTWORD ]]; then fzf-tab-complete; else zle complete-word; fi }
function fzf-tab-partial-and-complete() { CURRENTWORD="${LBUFFER/* /}${RBUFFER/ */}"; if [[ $LASTWIDGET = 'fzf-tab-partial-and-complete' || -z $CURRENTWORD ]]; then fzf-tab-expand-or-menu; else zle complete-word; fi }
function lsd-symbolic-link-dereference() { if [[ -h ${@: -1} && -d $( greadlink -f ${@: -1} ) ]]; then lsd ${@:1:${#}-1} $( greadlink -f ${@: -1} ); else lsd ${@}; fi }
function up-line-or-history() { zle set-local-history 1; zle .up-line-or-history; zle set-local-history 0; }
function down-line-or-history() { zle set-local-history 1; zle .down-line-or-history; zle set-local-history 0; }

## zstyle
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' menu select

## zle
zle -N fzf-tab-partial-and-complete
zle -N up-line-or-history
zle -N down-line-or-history

add-zsh-hook precmd iterm2-kubernetes-context

zle_highlight=(region:bold special:bold suffix:bold isearch:underline paste:bold)

[[ ${ITERM_SESSION_ID:3:1} -eq 0 && -x $(command -v neofetch) ]] && neofetch || true
